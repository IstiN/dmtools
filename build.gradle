plugins {
    id 'java-library'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'jacoco'
    id 'maven-publish'
    id 'signing'
    id 'org.jreleaser' version '1.9.0'
}

ext {
    isReleaseVersion = !version.endsWith("SNAPSHOT")
    // Extract version without 'v' prefix for JReleaser
    semVersion = version.startsWith('v') ? version.substring(1) : version
}

group = 'com.github.istin'
version = "${version}" // Keep original version with 'v' prefix

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }
}

test {
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        exceptionFormat "full"
    }
    finalizedBy jacocoTestReport
}

sourceSets {
    integrationTest {
        java.srcDir file('src/integrationTest/java')
        resources.srcDir file('src/integrationTest/resources')
        compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
        runtimeClasspath += output + compileClasspath
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

task integrationTest(type: Test) {
    description = 'Runs integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    shouldRunAfter test
}

check.dependsOn integrationTest

def log4jVersion = '2.20.0'
def slf4jVersion = '1.7.36'

dependencies {
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:5.14.2'
    testImplementation 'org.mockito:mockito-inline:5.2.0'
    testImplementation 'org.powermock:powermock-module-junit4:2.0.9'
    testImplementation 'org.powermock:powermock-api-mockito2:2.0.9'
    testRuntimeOnly("org.junit.vintage:junit-vintage-engine:5.8.2")

    api 'org.json:json:20231013'
    api 'com.squareup.okhttp3:okhttp:4.12.0'
    api 'org.apache.commons:commons-lang3:3.8.1'
    api 'com.thedeanda:lorem:2.1'
    api "org.apache.logging.log4j:log4j-api:${log4jVersion}"
    api "org.apache.logging.log4j:log4j-core:${log4jVersion}"
    api "org.apache.logging.log4j:log4j-slf4j-impl:${log4jVersion}"
    api "org.slf4j:slf4j-api:${slf4jVersion}"
    api group: 'commons-io', name: 'commons-io', version: '2.14.0'
    api group: 'commons-codec', name: 'commons-codec', version: '1.13'
    api group: 'org.freemarker', name: 'freemarker', version: '2.3.30'
    api(group: 'org.jsoup', name: 'jsoup', version: '1.18.3')
    api group: 'org.jxls', name: 'jxls', version: '2.14.0'
    api group: 'org.jxls', name: 'jxls-poi', version: '2.14.0'
    api group: 'org.jxls', name: 'jxls-reader', version: '2.1.0'
    api(group: 'org.apache.commons', name: 'commons-collections4', version: '4.3')
    api(group: 'org.apache.commons', name: 'commons-jexl', version: '2.1.1')
    api group: 'org.apache.poi', name: 'poi', version: '4.1.1'
    api group: 'org.apache.poi', name: 'poi-ooxml', version: '3.17'
    api 'org.apache.pdfbox:pdfbox:3.0.2'
    api 'io.github.furstenheim:copy_down:1.1'
    api 'org.seleniumhq.selenium:selenium-java:4.21.0'
    api 'io.github.bonigarcia:webdrivermanager:5.9.2'
    api group: 'org.graalvm.js', name: 'js', version: '24.1.1'
    api group: 'org.graalvm.js', name: 'js-scriptengine', version: '24.1.1'
    api 'com.github.mpkorstanje:simmetrics-core:4.1.1'
    api 'com.google.dagger:dagger:2.53.1'
    api 'com.google.auth:google-auth-library-oauth2-http:1.19.0'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.53.1'
    compileOnly 'org.projectlombok:lombok:1.18.36'
    annotationProcessor 'org.projectlombok:lombok:1.18.36'
    api 'net.sourceforge.tess4j:tess4j:5.8.0'
    api 'org.bytedeco:javacpp:1.5.11'
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.group == 'org.slf4j') {
            details.useVersion slf4jVersion
        }
        if (details.requested.group == 'org.apache.logging.log4j') {
            details.useVersion log4jVersion
        }
    }
}

test {
    useJUnitPlatform()
}

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'com.github.istin'
            artifactId = 'dmtools'
            version = "${version}" // Keep original version with 'v' prefix
            from components.java

            pom {
                name = 'DM Tools'
                description = 'DM Tools Library'
                url = 'https://github.com/IstiN/dmtools'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'istin'
                        name = 'Uladzimir Klyshevich'
                        email = 'istin2007@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/IstiN/dmtools.git'
                    developerConnection = 'scm:git:ssh://github.com:IstiN/dmtools.git'
                    url = 'https://github.com/IstiN/dmtools'
                }
            }
        }
    }
}

tasks.register('prepareJReleaser') {
    doLast {
        mkdir("${buildDir}/jreleaser")
        mkdir("${buildDir}/staging-deploy")
    }
}

jreleaser {
    project {
        name = 'dmtools'
        version = semVersion // Use version without 'v' prefix for JReleaser
        website = 'https://github.com/IstiN/dmtools'
        description = 'DM Tools Library'
        license = 'MIT'
        copyright = 'Copyright Â© 2024 Uladzimir Klyshevich. All rights reserved.'
        authors = ['Uladzimir Klyshevich']
        java {
            version = '23'
        }
    }

    signing {
        active = 'ALWAYS'
        armored = true
    }

    release {
        github {
            owner = 'IstiN'
            name = 'dmtools'
            tagName = "v${project.version}"
            releaseName = "v${project.version}"
            draft = false
            prerelease = false
            overwrite = true
            changelog {
                enabled = true
                formatted = true
            }
        }
    }

    deploy {
        maven {
            nexus2 {
                create('maven-central') {
                    active = 'ALWAYS'
                    url = 'https://s01.oss.sonatype.org/service/local/'
                    snapshotUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
                    closeRepository = true
                    releaseRepository = true
                    stagingRepositories = ["${buildDir}/staging-deploy"]
                }
            }
        }
    }

    distributions {
        main {
            artifacts {
                artifact {
                    path = "${buildDir}/libs/dmtools-${project.version}-all.jar"
                }
            }
        }
    }
}

tasks.register('prepareJReleaser') {
    doLast {
        mkdir("${buildDir}/jreleaser")
        mkdir("${buildDir}/staging-deploy")
    }
}

tasks.named('jreleaserDeploy').configure {
    dependsOn 'prepareJReleaser', 'shadowJar'
}

tasks.named('jreleaserPackage').configure {
    dependsOn 'prepareJReleaser', 'shadowJar'
}

signing {
    def signingKey = System.getenv("GPG_SIGNING_KEY")
    def signingPassword = System.getenv("GPG_SIGNING_PASSWORD")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.maven
}

shadowJar {
    archiveBaseName.set('dmtools')
    archiveVersion.set(project.version) // Keep original version with 'v' prefix
    archiveClassifier.set('all')
    archiveExtension.set('jar')
    manifest {
        attributes 'Main-Class': 'com.github.istin.dmtools.job.JobRunner'
    }
}